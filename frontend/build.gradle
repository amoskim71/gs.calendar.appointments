plugins {
    id "kotlin2js"
    id "org.jetbrains.kotlin.frontend" version "0.0.45"
}

version "0.0.1"

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    maven { url "https://dl.bintray.com/kotlinx/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/kotlin-js-wrappers" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"
    compile "org.jetbrains:kotlin-react:$reactVersion-pre.69-kotlin-$kotlinVersion"
    compile "org.jetbrains:kotlin-react-dom:$reactVersion-pre.69-kotlin-$kotlinVersion"

    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion"
}

def deployDir = file("$buildDir/static")

kotlinFrontend {
    npm {
        dependency("webpack-cli", "v2.0.12")
        dependency("react", reactVersion)
        dependency("react-dom", reactVersion)
        dependency("@material-ui/core", "3.9.2")
    }

    sourceMaps = true

    webpackBundle {
        bundleName = "main"
        contentPath = deployDir
        proxyUrl = "http://localhost:8080"
    }
}

compileKotlin2Js {
    kotlinOptions {
        metaInfo = true
        outputFile = "$buildDir/js/${name}.js"
        sourceMap = true
        moduleKind = "commonjs"
        main = "call"
    }
}

task assembleStaticBundle(type: Copy, dependsOn: "bundle") { self ->
    from "$buildDir/bundle", "$projectDir/src/main/web"
    into deployDir
    doFirst { delete self.outputs }

    build.dependsOn self
}
